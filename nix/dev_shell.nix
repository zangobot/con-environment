{ inputs, pkgs, system, hostSystemName, ... }:
let 
  inherit (inputs.services-flake.lib) multiService;
  inherit (inputs) fenix;
  inherit (inputs) nix-kube-generators;
  kubelib = nix-kube-generators.lib { inherit pkgs; };

  rustToolchain = fenix.packages.${system}.stable.toolchain;

  isDarwin = pkgs.stdenv.isDarwin;
  isLinux = pkgs.stdenv.isLinux;

  mkContainerScripts = import ./container_scripts.nix { 
    inherit pkgs; 
  };

  talosConfigs = import ./talos-config.nix { 
    inherit pkgs inputs; 
    lib = pkgs.lib;
    clusterName = "aivProd";
    talosVersion = "v1.12.1";
    vIp = "10.211.0.20";
    nfsServer = "10.211.0.10";
    mainPath = "/mnt/data/dynamic-pvc";
    vllmPath = "/mnt/data/model-store";
  };
  
  talosPxe = import ./nas/talos-image.nix { 
    inherit pkgs; 
  } {
    version = "v1.12.1";
    platform = "metal";
    arch = "amd64";
    systemExtensions = [
      "siderolabs/amd-ucode"
      "siderolabs/intel-ucode"
      "siderolabs/nvidia-container-toolkit-lts"
      "siderolabs/nvidia-open-gpu-kernel-modules-lts"
    ];
    sha256 = "sha256-ctKKY9stHhMosgyKCDWQVMzOxv0wPnqsRitZlkhxYpY=";

    # sha256 = pkgs.lib.fakeHash;

    diskImage = "pxe-assets";
  };

  myContainerScripts = mkContainerScripts [
    # Standard Dockerfile builds (type="docker" is default)
    {
      name = "yolo-l2-notebook";
      path = "workshops/yolo-l2/notebook";
    }
    {
      name = "yolo-l2-verification";
      path = "workshops/yolo-l2/verification";
    }
    {
      name = "email-indirect-service";
      path = "workshops/email-indirect/service";
    }
    {
      name = "email-indirect-user";
      path = "workshops/email-indirect/user";
    }

    # Nix builds - core components
    { 
      name = "workshop-sidecar"; 
      path = "workshop-sidecar"; 
      type = "nix"; 
    }
    { 
      name = "workshop-hub"; 
      path = "workshop-hub"; 
      type = "nix"; 
    }
  ];

  cliTools = with pkgs; [
    curl
    talosctl
    kubectl
    kubernetes-helm
    tilt
    openssl
    zsh
    k9s
    cilium-cli
    hubble
    sops
    ssh-to-age
  ] ++ myContainerScripts ++ [ rustToolchain talosConfigs talosPxe ];
in
{
  shell = pkgs.mkShell {
      name = "aiv-k8-dev";

      # The packages available in the development environment
      packages = cliTools;

      # Setup hook that prepares environment and config files
      shellHook = ''
        ${if isDarwin then ''
          # macOS-specific configuration
          unset DEVELOPER_DIR
        '' else ""}

        # Set up environment variables
        export PROJECT_ROOT=$PWD
        export DATA_DIR="$PROJECT_ROOT/.data"

        if [ -f .envhost ]; then
          set -a
          source .envhost
          set +a
          if [ -n "$GITHUB_USERNAME" ] && [ -n "$GHCR_PAT" ]; then
            echo "Logging into ghcr.io..."
            echo "$GHCR_PAT" | docker login ghcr.io -u "$GITHUB_USERNAME" --password-stdin
          fi
        fi
        # Todo: move this elsewhere
        export TALOS_VERSION="v1.11.0"
        export KUBECONFIG="$DATA_DIR/talos/kubeconfig"
        export TALOSCONFIG="$DATA_DIR/talos/talosconfig"
        export TALOS_STATE_DIR="$DATA_DIR/talos"
        export DIRENV_WARN_TIMEOUT=0
        export TF_DATA_DIR="$PROJECT_ROOT/.data/terraform"
        export TF_VAR_kubeconfig="$KUBECONFIG"
        export MC_CONFIG_DIR="$PROJECT_ROOT/.data/minio"
        export LD_LIBRARY_PATH="${pkgs.lib.makeLibraryPath [ pkgs.openssl ]}:$LD_LIBRARY_PATH"
      '';
  };

  conShell = pkgs.mkShell {
      name = "aiv-k8-dev";

      # The packages available in the development environment
      packages = cliTools;

      # Setup hook that prepares environment and config files
      shellHook = ''
        ${if isDarwin then ''
          # macOS-specific configuration
          unset DEVELOPER_DIR
        '' else ""}

        # Set up environment variables
        export PROJECT_ROOT=$PWD
        export DEPLOYMENT_DIR="$PROJECT_ROOT/deployment"

        if [ -f .envhost ]; then
          set -a
          source .envhost
          set +a
          if [ -n "$GITHUB_USERNAME" ] && [ -n "$GHCR_PAT" ]; then
            echo "Logging into ghcr.io..."
            echo "$GHCR_PAT" | docker login ghcr.io -u "$GITHUB_USERNAME" --password-stdin
          fi
        fi
        # Todo: move this elsewhere
        export TALOS_VERSION="v1.12.1"
        export KUBECONFIG="$DEPLOYMENT_DIR/talos/kubeconfig"
        export TALOSCONFIG="$DEPLOYMENT_DIR/talos/talosconfig"
        export TALOS_STATE_DIR="$DEPLOYMENT_DIR/talos"
        export LD_LIBRARY_PATH="${pkgs.lib.makeLibraryPath [ pkgs.openssl ]}:$LD_LIBRARY_PATH"
      '';
  };

  environment = {
    imports = [
      inputs.services-flake.processComposeModules.default
      (multiService ./dev_shell/tilt.nix)
      (multiService ./dev_shell/local_path_storage.nix)
      (multiService ./dev_shell/talos.nix)
      (multiService ./dev_shell/patches.nix)
      (multiService ./dev_shell/container_repository.nix)
    ];
    
    services = {
      container_repository = {
        docker = {
          enable = true;
          remoteUrl = "https://registry-1.docker.io";
          dataDir = ".data/repo/docker";
          localPort = 5000;
        };
        k8s = {
          enable = true;
          remoteUrl = "https://registry.k8s.io";
          dataDir = ".data/repo/k8s";
          localPort = 5001;
        };
        gcr = {
          enable = true;
          remoteUrl = "https://gcr.io";
          dataDir = ".data/repo/gcr";
          localPort = 5002;
        };
        ghcr = {
          enable = true;
          remoteUrl = "https://ghcr.io";
          dataDir = ".data/repo/ghcr";
          localPort = 5003;
        };
        quay = {
          enable = true;
          remoteUrl = "https://quay.io";
          dataDir = ".data/repo/quay";
          localPort = 5004;
        };
      };

      patches."patch0" = {
        enable = true;
        ciliumValuesFile = ../setup/k8/cilium-values.yaml;
        dataDir = ".data/talos-patches";
        kubelib = kubelib;
      };

      talos = {
        cluster = {
          enable = true;
          useSudo = true;
          dataDir = ".data/talos";
          controlplanes = 1;
          cpus = "4.0";
          memory = 8192;
          workers = 3;
          cpusWorkers = "4.0";
          memoryWorkers = 12188;
          disk = 24376;
          # extra-disks = 2;
          # extra-disks-size = 8192;
          provisioner = "qemu";
          registryMirrors = [
            "docker.io=http://10.5.0.1:5000"
            "registry.k8s.io=http://10.5.0.1:5001"
            "gcr.io=http://10.5.0.1:5002"
            "ghcr.io=http://10.5.0.1:5003"
            "quay.io=http://10.5.0.1:5004"
          ];
          # This is defined in the .envrc. These can't be paths as they're not checked in.
          configPatches = [
            ".data/talos-patches/cilium.yaml"
            ".data/talos-patches/ghcr.yaml"
          ];
        };
      };

      local_path_storage."storage" = {
        enable = true;
        kubeconfig = ".data/talos/kubeconfig";
      };
      
      tilt = {
        tilt = {
          enable = true;
          dataDir = ".data/postgres";
          hostname = hostSystemName;
          runtimeInputs = [];
          environment = {
            KUBECONFIG = ".data/talos/kubeconfig";
            HOSTNAME = hostSystemName;
            NIX_CONFIG = "experimental-features = nix-command flakes";
            NIX_PATH = "nixpkgs=${pkgs.path}";
          };
        };
      };
    };
    
    settings.processes.cluster.depends_on = {
      docker.condition = "process_started";
      k8s.condition = "process_started";
      gcr.condition = "process_started";
      ghcr.condition = "process_started";
      patch0.condition = "process_completed_successfully";
    };
    settings.processes.storage.depends_on = {
      cluster.condition = "process_log_ready";
    };
    settings.processes.tilt.depends_on = {
      storage.condition = "process_completed_successfully";
      cluster.condition = "process_log_ready";
    };
  };
}